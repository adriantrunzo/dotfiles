" No vi compatibility.
set nocompatible

if has('osx')
  " Change the cursor shape depending upon the mode.
  " https://vim.fandom.com/wiki/Change_cursor_shape_in_different_modes
  let &t_SI.="\e[5 q" "SI = INSERT mode
  let &t_SR.="\e[4 q" "SR = REPLACE mode
  let &t_EI.="\e[1 q" "EI = NORMAL mode (ELSE)
endif

" vim-polyglot includes vim-sensible by default. It needs to be disabled
" before the plugin is loaded.
let g:polyglot_disabled = ['sensible']

" Set the leader before plugins might add their own mappings.
let mapleader = ' '

" Install vim-plug if not found
let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
if empty(glob(data_dir . '/autoload/plug.vim'))
  silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs
        \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

" Run PlugInstall if there are missing plugins
autocmd VimEnter * if len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
      \| PlugInstall --sync | source $MYVIMRC
      \| endif

call plug#begin()

Plug 'airblade/vim-gitgutter'
Plug 'andymass/vim-matchup'
Plug 'antoinemadec/coc-fzf'
Plug 'dracula/vim', { 'as': 'dracula' }
Plug 'junegunn/fzf'
Plug 'junegunn/fzf.vim'
Plug 'justinmk/vim-sneak'
Plug 'lambdalisue/fern.vim'
Plug 'machakann/vim-highlightedyank'
Plug 'machakann/vim-sandwich'
Plug 'neoclide/coc.nvim', { 'branch': 'release' }
Plug 'sheerun/vim-polyglot'
Plug 'stsewd/fzf-checkout.vim'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-fugitive'
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'

call plug#end()

" Use vim-surround mappings to not conflict with vim-sneak.
runtime macros/sandwich/keymap/surround.vim

" Enable filetype and indent detection.
filetype plugin indent on

" Enable syntax highlighting.
syntax enable

" Use the Dracula color scheme.
colorscheme dracula

" Create new lines at the same indentation level.
set autoindent

" Detect file changes outside of vim and reload those files if not changed.
set autoread

" Use colors suitable for a dark background. The colorscheme should also set
" this value.
set background=dark

" Allow backspacing over everything in insert mode.
set backspace=indent,eol,start

" Don't put backup files in the current directory.
set backupdir^=$HOME/.vim/backup//

" Don't ring the bell.
set belloff=all

" Highlight the column after textwidth.
set colorcolumn=+1

" Don't put swap files in the current directory.
set directory^=$HOME/.vim/swap//

" Use utf-8 by default.
set encoding=utf-8

" Expand tabs to spaces in insert mode.
set expandtab

" Remove comment leader when joining lines.
set formatoptions+=j

" Allow for hidden buffers. Necessary for some plugins.
set hidden

" Highlight search results.
set hlsearch

" Ignore case when searching.
set ignorecase

" Show matches as search proceeds.
set incsearch

" Show line numbers.
set number

" Make the line numbers relative.
set relativenumber

" Always show lines above and below cursor when scrolling.
set scrolloff=5

" Always show the signcolumn, otherwise it would shift the text each time
" diagnostics appear/become resolved.
set signcolumn=yes

" Use two spaces when indenting.
set shiftwidth=2

" Don't pass messages to |ins-completion-menu|.
set shortmess+=c

" Show a symbol for wrapped lines.
set showbreak=â†ª

" Show partial commands in the last line.
set showcmd

" Don't show the mode as vim-airline will already display it for us.
set noshowmode

" Make search case-sensitive if capital letters are used.
set smartcase

" Use two spaces for the tab key.
set softtabstop=2

" Open new splits in a more "natural" direction; below for horizontal splits and
" to the right for vertical splits.
set splitbelow
set splitright

" Enable true color.
set termguicolors

" Break text after 80 characters.
set textwidth=80

" Time out for key codes.
set ttimeout

" Wait up to 100ms after Esc for special key.
set ttimeoutlen=100

" Set the title of the terminal window appropriately.
set title

" Don't put undo files in the current directory.
set undodir^=$HOME/.vim/undo//

" Persistent undo.
set undofile

" coc.nvim recommends a lower updatetime, which is used by the highlight on
" CursorHold feature.
set updatetime=500

" Enable wildmenu (command-line completion).
set wildmenu

" List all matches and complete the first match.
set wildmode=list:full

" Wrap long lines.
set wrap

" Resize splits when the window is resized.
au VimResized * :wincmd =

augroup Typescript
    autocmd!
    " The .tsx extension isn't always used with react files.
    autocmd BufNewFile,BufRead *.ts set filetype=typescriptreact
augroup END

" Use jj for quick escape.
inoremap jj <esc>

" Better redo.
nnoremap U <c-r>

" Make Y behave as expected, though don't yank the trailing whitespace.
nnoremap Y yg_

" Reselect visual selection after indenting.
vnoremap < <gv
vnoremap > >gv

" Maintain the cursor position when yanking a visual selection.
" http://ddrscott.github.io/blog/2016/yank-without-jank/
vnoremap y myy`y
vnoremap Y myY`y

" Quickly list all available buffers.
nnoremap gb :ls<cr>:b<space>

nnoremap [f <cmd>bprevious<cr>
nnoremap <left> <cmd>bprevious<cr>
nnoremap ]f <cmd>bnext<cr>
nnoremap <right> <cmd>bnext<cr>

" System clipboard copy/paste.
nnoremap <leader>cp "*y
nnoremap <leader>cv "*gp
vnoremap <leader>cp "*y
vnoremap <leader>cv "*gp

" Change word and use dot to repeat on next occurrence.
nnoremap <leader>cn *``cgn
nnoremap <leader>cN *``cgN

" Quickly clear search highlighting.
nnoremap <leader>k <cmd>noh<cr>

" Easier quit.
nnoremap <leader>q <cmd>quit<cr>

" Editing vim configuration.
nnoremap <leader>vc <cmd>edit $MYVIMRC<cr>
nnoremap <leader>vr <cmd>source $MYVIMRC<cr>

" Git
nnoremap <leader>b <cmd>GBranches<cr>
nnoremap <leader>g <cmd>Git<cr>

" File explorer.
nnoremap <leader>e <cmd>Fern . -reveal=%<cr>

" Fuzzy searching.
nnoremap <leader>f <cmd>Rg<cr>
nnoremap <leader>o <cmd>GFiles --exclude-standard --others --cached<cr>

" Quick save (update only writes if there are changes).
nnoremap <leader>l <cmd>bdelete<cr>
nnoremap <leader>s <cmd>update<cr>
 
" LSP
nmap <leader>ca <Plug>(coc-codeaction)
nmap <leader>cc <Plug>(coc-declaration)
nmap <leader>cd <plug>(coc-definition)
xmap <leader>cf <plug>(coc-format-selected)
nmap <leader>cf <plug>(coc-format-selected)
nmap <leader>ci <plug>(coc-implementation)
nmap <leader>ct <plug>(coc-type-definition)
nmap <leader>fq <plug>(coc-fix-current)
nnoremap <silent> K <cmd>call <sid>show_documentation()<cr>
nmap <leader>rn <plug>(coc-rename)
nmap <leader>rr <plug>(coc-references)

nmap ]d <plug>(coc-diagnostic-next)
nmap [d <plug>(coc-diagnostic-prev)

" Allow scrolling of the floating LSP windows with <c-f> and <c-b>.
nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
inoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
inoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
vnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
vnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"

" Make the fuzzy finder floating window bigger.
let g:fzf_layout = { 'window': { 'width': 0.8, 'height': 0.8 } }
let $FZF_DEFAULT_OPTS = '--info=inline'

" CoC Extensions. These should be automatically installed by coc.nvim.
let g:coc_global_extensions = [
      \ "coc-css",
      \ 'coc-cssmodules',
      \ 'coc-eslint',
      \ 'coc-html',
      \ 'coc-json',
      \ 'coc-pairs',
      \ 'coc-prettier',
      \ 'coc-tsserver',
      \ 'coc-vimlsp'
      \ ]

" Show documentation in preview window.
function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif (coc#rpc#ready())
    call CocActionAsync('doHover')
  else
    execute '!' . &keywordprg . " " . expand('<cword>')
  endif
endfunction

" Trigger completions with enter.
" inoremap <expr> <cr> pumvisible() ? "\<C-y>" : "\<C-g>u\<cr>"
inoremap <silent><expr> <cr> pumvisible() ? coc#_select_confirm()
      \: "\<c-g>u\<cr>\<c-r>=coc#on_enter()\<cr>"
" :h complete_CTRL-Y
" :h i_CTRL-G_u
" :h i_CTRL-R
" :h index

" Navigate completions with Tab.
inoremap <expr> <Tab> pumvisible() ? "\<C-n>" : "\<Tab>"
inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"

" Highlight the symbol and its references when holding the cursor.
autocmd CursorHold * silent call CocActionAsync('highlight')

" Use dracula and keep it simple.
let g:airline_theme='dracula'
let g:airline_symbols_ascii = 1

if !exists('g:airline_symbols')
  let g:airline_symbols = {}
endif

" Remove the space before the line number prefix since we are removing the
" percentage indicator (see below).
let g:airline_symbols.linenr = 'ln:'

" Disable most extensions by default.
let g:airline_extensions = ['branch', 'coc']
let g:airline#extensions#nerdtree_statusline = 1

" Use a custom function to get the git branch text.
let g:airline#extensions#branch#format = 'GetGitBranch'

function! GetGitBranch(name)
  " Shortcut/Clubhouse git branches will contain the ticket identifier preceeded
  " by either "ch" or "sc-".
  let story = matchstr(a:name, '\(ch\|sc-\)\d\+')

  " Use the identifier segment if we found one.
  if (strlen(story) > 0)
    return story[:strlen(story) - 1]
  endif

  " Otherwise the first ten characters.
  return a:name[:9]
endfunction

" Remove the document percentage indicator.
function! AirlineInit()
  let g:airline_section_z = airline#section#create(['linenr', 'maxlinenr', 'colnr'])
endfunction
autocmd User AirlineAfterInit call AirlineInit()

" More colorful JSX.
let g:vim_jsx_pretty_colorful_config = 1

function! FernInit() abort
  nmap <buffer><expr>
      \ <Plug>(fern-my-open-expand-collapse)
      \ fern#smart#leaf(
      \   "\<Plug>(fern-action-open)",
      \   "\<Plug>(fern-action-expand)",
      \   "\<Plug>(fern-action-collapse)",
      \ )

  nmap <buffer> <CR> <Plug>(fern-my-open-expand-collapse)
  " nmap <buffer> <2-LeftMouse> <Plug>(fern-my-open-expand-collapse)
  " nmap <buffer> m <Plug>(fern-action-mark:toggle)j
  " nmap <buffer> N <Plug>(fern-action-new-file)
  " nmap <buffer> K <Plug>(fern-action-new-dir)
  " nmap <buffer> D <Plug>(fern-action-remove)
  " nmap <buffer> V <Plug>(fern-action-move)
  " nmap <buffer> R <Plug>(fern-action-rename)
  " nmap <buffer> s <Plug>(fern-action-open:split)
  " nmap <buffer> v <Plug>(fern-action-open:vsplit)
  " nmap <buffer> r <Plug>(fern-action-reload)
  " nmap <buffer> <nowait> d <Plug>(fern-action-hidden:toggle)
  " nmap <buffer> <nowait> < <Plug>(fern-action-leave)
  " nmap <buffer> <nowait> > <Plug>(fern-action-enter)
endfunction

augroup FernEvents
  autocmd!
  autocmd FileType fern call FernInit()
augroup END

" Alerting offscreen matches messes with airline.
let g:matchup_matchparen_offscreen = {}

